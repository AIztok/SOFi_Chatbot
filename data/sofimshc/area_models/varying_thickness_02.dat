!#!Info  Example:       Varying thickness using points thicknesses
!#!Info  Keyword:       Structural model; area elements
!#!Info  Program:       SOFIMSHC
!#!Info  Version:       SOFiSTiK 25
!#!Info  Date:          $Date: 2009-04-22 02:04:38 +0200 (Di, 31 Mrz 2009) $

+prog aqua urs:1
head material
norm DIN 1045-1
conc 1 C 35
conc 2 C 30
end

+prog sofimshc urs:4
head varying thickness
syst SPAC gdiv 10000 POSZ $ 3D spacial system

ctrl mesh 2     $ create quadrilateral elements
ctrl hmin 1.0   $ global mesh density

$ structural lines
sln 1        ; slnb x1  0 0 0  x2 10 0 0
sln 2 fix PP ; slnb x1 10 0 0  x2 10 5 0
sln 3        ; slnb x1 10 5 0  x2  0 5 0
sln 4 fix PP ; slnb x1  0 5 0  x2  0 0 0

sln 5 fix PP ; slnb x1 10  5 0  x2 10 10 0
sln 6        ; slnb x1 10 10 0  x2  0 10 0
sln 7 fix PP ; slnb x1  0 10 0  x2  0  5 0

sar 1 t 0.0 mno 1 $ thickness must be set explicitly to 0.0!
sarb out nl 1,2,3,4

sar 2 t 0.2 mno 1 mctl 1
sarb out nl 3,5,6,7

$ points in area 1 with thickness values
spt 101 x  0 0 0  T 0.2
spt 102 x 10 0 0  T 0.2
spt 103 x 10 5 0  T 0.2
spt 104 x  0 5 0  T 0.2

spt 105 x 5 1 0 T 1.2
spt 106 x 5 4 0 T 1.2
end
