!#!Info  Example:       Solid Body with holes meshed with bric elements
!#!Info  Keyword:       Meshing; Brics; Hexahedral elements
!#!Info  Program:       SOFIMSHC,SOFIMSHA
!#!Info  Version:       SOFiSTiK 2016

+prog aqua urs:2
head Material
norm DIN 18800
stee 1 S 235
end

prog sofimshc m8000000 urs:1
head 2D Quad meshes
page UNII 7      $ mechanical units: all in [mm]

syst 3D gdiv 10000
ctrl TOLG 0.001  $ adapt geometric tolerance to model size
ctrl mesh 2      $ create mesh
ctrl hmin 8.0[mm] $ mesh size

$ Parameters
let#m  64,220,300,80,16  $ total dimensions = height,length,width,thick1,thick2

let#d  28             $ diameter of openings
let#h1 -60,0,60       $ x-position of openings in handle
let#h2 260            $ y-position of openings in cantilever

let#B  #m(1)/2,#m(1)/2-#m(4)

$ Points
spt 1   #(B+1) #(m+3)  0.0
    2   #(B+0) #(m+3)  0.0
    3   #(B+0)   0.0   0.0
    4  -#(B+0)   0.0   0.0
    5  -#(B+0) #(m+3)  0.0
    6  -#(B+1) #(m+3)  0.0
    7  -#(B+0) #(m+3)  #(m)
    8  -#(B+0) #(m+2)  #(m)
    9  -#(B+0) #(m+2)  0.0

$ Lines
sln 1 6 1
    2 1 2
    3 2 3
    4 3 4
    5 4 5
    6 5 6

sln 11 5 7
    12 7 8
    13 8 9
    14 9 5

$ Openings
sln 21 ; slnb xm #h1(0) #m(3)/2 R #d/2 nz 1
sln 22 ; slnb xm #h1(1) #m(3)/2 R #d/2 nz 1
sln 23 ; slnb xm #h1(2) #m(3)/2 R #d/2 nz 1

sln 31 fix PP ; slnb xm -#B #h2(0) #m(0)/2 R #d/2 nx 1

$ Structural areas
sar 100 t 0
sarb out 1,2,3,4,5,6
sarb in 21,22,23

sar 200 t 0
sarb out 11,12,13,14
sarb in 31
end

+prog sofimsha urs:3
head Extrusion to Bric mesh
syst REST

$ Extrusion of quad elements to brics
bric PROP mno 1
grp 11 ; extr QUAD from SAR 100 path SLN 11
grp 12 ; extr QUAD from SAR 200 path SLN 6
grp 13 ; extr QUAD from SAR 200 path SLN 2 sref 5

$ Extrusion of support conditions in opening of cantilever
extr FIX from SLN 31 path SLN 6
extr FIX from SLN 31 path SLN 2 sref 5
end


