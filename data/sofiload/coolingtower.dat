+PROG AQUA
HEAD Cooling Tower
NORM DIN EN199X-200X
CONC 1 C 30 ; STEE 2 B 500A
SREC 1 H 1800 B 600 SO 50[mm]
SVAL 99 A 0.001 IY 0.1 IZ .00000001 $ weak about the z-axis
END

+PROG SOFIMSHA
HEAD Cooling Tower
SYST SPAC GDIR NEGZ GDIV 10000
$ Meridian as Hyperbolic equation (r/r0)**2 - ((z-z0)/zz)**2 = 1
$   (r1/r0)**2 - (-z0/zz)**2    = 1  => (1/zz)**2 = ((r1/r0)**2-1.)/z0**2
$   (r2/r0)**2 - ((h-z0)/zz)**2 = 1
$ parameter:
let#H   160.0 $ Total Height
let#R0   30.0 $ smallest radius
let#R1   55.0 $ Radius at bottom
let#R2   35.0 $ Radius at top
let#D1    400 $ Thickness at bottom
let#D2    220 $ Thickness at top
let#1   SQR(#r1**2-#r0**2)
LET#Z0  #H*#1*(SQR(#R2**2-#R0**2)-#1)/(#R2**2-#R1**2)
let#zz  #z0/SQR((#r1/#r0)**2-1.0)
let#m    16   $ subdivision in height
let#n    32   $ subdivision in periphery (<50)
TXB Generation of a hyperbolic cooling tower with following parameters
TXB      height       radius      thickness
TXB         0.0    #(r1,9.1)      #(d1,9.3)
TXB   #(z0,9.1)    #(r0,9.1)
TXB    #(h,8.1)    #(r2,9.1)      #(d2,9.3)
TXB
TXB Parameter zz of hyperbolic equation = #(zz,9.3)
$
let#2 #H/#m   $ delta-z
LOOP#1 #m+1 ; let#z #1*#2
   NODE #1*50+1  #r0*sqr(1.+((#z-#z0)/#zz)**2) 0.0 #z
ENDLOOP
LOOP#1 #n-1
   TRAN NODE 1 1+(#m+1)*50 50 PHIZ (#1+1)*360/#n DNO (#1+1)
endloop
$
loop#1 #m ; let#2 #1*50  $ offset of reference corner
   let#d #d1+#1/#m*(#d2-#d1)
   loop#3 #n-1
      QUAD #2+1+#3  #2+1+#3  #2+2+#3 #2+52+#3 #2+51+#3 MNO 1 T #D
   endloop
   QUAD #2+#n #2+#n #2+1 #2+51 #2+#n+50 MNO 1 T #D
endloop
NODE (1001 1032 1) 2.50 NR1 (51 1)   (1 1)    'SS'
NODE (2001 2031 1) 0.50 NR1 (1001 1) (1002 1) 'SS'
NODE  2032         0.50 NR1  1032     1001    'SS'
NODE 999 0 0 0 $ reference node
GRP 1 TITL 'Inflow Region'
BEAM (  1  32 1)  (1 1) (2001 1) KR 999 NP -1 NCS 1
BEAM (101 131 1)  (2 1) (2001 1) KR 999 NP -1 NCS 1
BEAM  132          1     2032    KR 999 NP -1 NCS 1
GRP 2 TITL 'Connectors'
BEAM (51 82 1) (51 1) (1 1)  KR ZZ NP -1 NCS 99 $ due to inplane freedom of th shell
NODE (2001 2032 1) FIX pp
END


+PROG SOFILOAD
HEAD WIND LOADING
ECHO LOAD
LC 1 DLZ -1.0 TITL 'dead load'
$
GRP 0,1 WIND GUST
GRP 2
LC 50 TITL 'Wind via Reynolds number'
WIND 1055 3 B DX 1.0
BEAM GRP 1 TYPE WIND 1.0
QUAD GRP 0 TYPE WIND 0.8 NTYP CYL 60
LC 51 TITL 'Wind via Reynolds number'
WIND 1055 3 B DX 1.0
BEAM GRP 1 TYPE WIND 1.0
QUAD GRP 0 TYPE WIND NTYP CYL 60
LC 52 TITL 'WIND via Diameter'
WIND 1055 3 B DX 1.0
BEAM GRP 1 TYPE WIND 1.0
QUAD GRP 0 TYPE WIND NTYP CYLR 1E7
LC 53 TITL 'WIND via roughness'
WIND VGB 3 II DX 1.0
BEAM GRP 1 TYPE WIND 1.0
QUAD GRP 0 TYPE WIND NTYP CYLK 1.6
LC 61 TITL 'inner Temperature +30'
QUAD GRP 0   TYPE DTZ +30
BEAM GRP 1,2 TYPE DTY -30
END

+PROG ASE
HEAD Dead load Wind Temperature
ECHO DISP,REAC,FORC,NOST NO
CTRL PLAB 0
LC 1
LC 50,51,52,53
LC 61
END

+PROG WING
HEAD
SIZE lp 0 SPLI 1x2 SLEG 1
VIEW STAN 3 5 2 NEGZ
VISI HIDD LINE
STRU 0 0
VISI HIDD NO
VIEW TYPE DIRE X 0.4954312 Y 0.5993057 Z -0.6287929 AXIS NEGZ ROTA 0
LC 50; LOAD TYPE PRSZ SCHH NO FILL AREA REPR DTXT ETYP QUAD GTYP ELEM
LC 51; LOAD TYPE PRSZ SCHH NO FILL AREA REPR DTXT ETYP QUAD GTYP ELEM
LC 52; LOAD TYPE PRSZ SCHH NO FILL AREA REPR DTXT ETYP QUAD GTYP ELEM
LC 53; LOAD TYPE PRSZ SCHH NO FILL AREA REPR DTXT ETYP QUAD GTYP ELEM
VIEW STAN 3 5 2 NEGZ
QUAD HN,HM SCHH 0
BOX -99 -99 -99 99 1 999
QUAD HSL SCHH 0 UNIT 1
BOX -99 -1 -99 99 99 999
QUAD HSU SCHH 0 UNIT 1
END

