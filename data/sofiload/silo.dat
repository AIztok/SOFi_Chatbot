+PROG AQUA
HEAD silo with volume loading
NORM DIN EN199X-200X
CONC 1 C 30
STEE 2 B 500A
END

PROG SOFIMSHA
HEAD silo with volume loading
SYST SPAC  GDIV 10000
ECHO FULL NO
sto#rza  0.4/sqr(2.0)  $ components x=y of the sink radius
sto#rz   3.0/sqr(2.0)  $ components x=y of the cylinder radius for mesh generation
sto#rzl  3.0           $ radius cylinder for load generation
sto#hz  10.0           $ height cylinder
sto#hza  1.3           $ height sink zone
let#diva  4            $ subdivision sink  (even number necessary)
let#div  12            $ subdivision cylinder (even number necessary)
let#divi 14            $ subdivision inside sink zone
$
QUAD PROP MNO 1 T 250[mm] POSI BELO  $ silo properties
$ centre node
NODE  1  0  0  0
$ sink zone
$ corner points of the sink
NODE 12  #rza #rza 0 PHIZ   0
NODE 13  #rza #rza 0 PHIZ  90
NODE 14  #rza #rza 0 PHIZ 180
NODE 15  #rza #rza 0 PHIZ 270
$ centre nodes of the sink (for rounding off)
NODE 23  #rza #rza 0 PHIZ  45
NODE 34  #rza #rza 0 PHIZ 135
NODE 45  #rza #rza 0 PHIZ 225
NODE 52  #rza #rza 0 PHIZ 315
$ edges of the sink
BOUN 1 TITL '1st quadrant' ; BOUN NA 13 12 NM -23 DIV #diva
BOUN 2 TITL '2nd quadrant' ; BOUN NA 14 13 NM -34 DIV #diva
BOUN 3 TITL '3rd quadrant' ; BOUN NA 15 14 NM -45 DIV #diva
BOUN 4 TITL '4th quadrant' ; BOUN NA 12 15 NM -52 DIV #diva
$
$ cylinder zone
$ nodes at sink level  (in polar coordinates)
$ corner points of the cylinder
NODE 112  #rz #rz 0 PHIZ   0
NODE 113  #rz #rz 0 PHIZ  90
NODE 114  #rz #rz 0 PHIZ 180
NODE 115  #rz #rz 0 PHIZ 270
$ centre nodes of the czylinder (for rounding off)
NODE 116  #rz #rz 0 PHIZ  45
NODE 117  #rz #rz 0 PHIZ 135
NODE 118  #rz #rz 0 PHIZ 225
NODE 119  #rz #rz 0 PHIZ 315
$
$ transformation of the cylinder nodes to the height bottom level of the cylinder
TRAN NODE 112 119 INC 1 DZ -#hza DNO 100   $ target numbers increased about 100
$ edges of the cylinder
BOUN 11 TITL '1st quadrant' ; BOUN NA 212 213  NM -216 DIV #div
BOUN 12 TITL '2nd quadrant' ; BOUN NA 213 214  NM -217 DIV #div
BOUN 13 TITL '3rd quadrant' ; BOUN NA 214 215  NM -218 DIV #div
BOUN 14 TITL '4th quadrant' ; BOUN NA 215 212  NM -219 DIV #div
$ inner edge between sink and cylinder
BOUN 21 TITL 'inner edge' ; BOUN NA 12 212  DIV #divi
$
$ element mesh of the sink zone
QUAD FIT N1 12 13 213 212 M12 216 KR 1
QUAD FIT N1 13 14 214 213 M12 217 KR 1
QUAD FIT N1 14 15 215 214 M12 218 KR 1
QUAD FIT N1 15 12 212 215 M12 219 KR 1
$
$ cylinder wall
QUAD PROP MNO 1 T 250[mm] POSI BELO $ properties of the cylinder wall
TRAN NODE 11,12,13,14 INC FIT PROP X20 DZ -#hz  $ 11..14 are the edge numbers, X20 the number of the subdivisions
$
$ boundary conditions
NODE 212,213,214,215 FIX f
END

+PROG SOFILOAD
HEAD silo with volume loading
LC 1 $ FLUID PRESSURE GAM=10 SURFACE AT Z=h0
VOLU REF QGRP NO 0 TYPE PZ Z -(#hz+#hza-1) P1 10
$
LC 2 $ FLUID PRESSURE GAM=10 SURFACE AT Z=h0
VOLU REF QGRP NO 0 TYPE PZ Z -(#hz+#hza-1) P1 10   $$
      X2  -7 -7 -4  X3   0   0  -4 $$                   $ box for the half system
      X4 +14 -7 -3  X5 -14  +7  -3
$     X2  -7 -7 -4 X3  +7  +7 -4 $$                     $ box for the complete system
$     X4 +14 -7 -3 X5 -14  +7 -3
$
LC 3 $ FLUID PRESSURE WITH LOADINCREMET IN GLOBAL Z
QUAD GRP 0 TYPE PZ P 0.0 X 0.0 Y 0.0 Z -(#hz+#hza) DPZ 10  $ liquid level upper edge cylinder
$
LC 4 $ FLUID PRESSURE THE SIGN OF THE LOAD VALUE DEPENDS ON ORIENTATION
VOLU REF QGRP NO 0 TYPE PV P 20.0 X 0.0 Y 0.0 Z -9.0
LC 5 $ GRANULAR FILLING
VOLU REF QGRP NO 0 A/U #rzl/2 TYPE PV P 20.0 X 0.0 Y 0.0  Z -9.0 $$
                   P1 1.4 P2 0.55 P3 1.05 P4 0.45 P5 1.0
LC 6 $ HORIZONTAL EARTHQUAKE-LOADING
VOLU REF QGRP NO 0 TYPE PXPZ Z -(#hz+#hza) P1 10*0.4
$
LC 10 DLZ 1.0
$
LC 11 $ SPECIAL LOADINGS ON FULL SYSTEM
VOLU REF QGRP NO 0 TYPE PXY  P 10
LC 12 $ SPECIAL LOADINGS ON FULL SYSTEM
VOLU REF QGRP NO 0 TYPE PXYS P 10
LC 13 $ SPECIAL LOADINGS ON FULL SYSTEM
VOLU REF QGRP NO 0 TYPE PXYP P 10
LC 14 $ SPECIAL LOADINGS ON FULL SYSTEM
VOLU REF QGRP NO 0 TYPE PXXZ P 10 PROJ XXL
LC 15 $ SPECIAL LOADINGS ON FULL SYSTEM
VOLU REF QGRP NO 0 TYPE PXPZ P 10
$
LC 21 $ SPECIAL LOADINGS ON PARTIAL SYSTEM
VOLU REF QGRP NO 0 TYPE PXY  Z -5 P1 10
LC 22 $ SPECIAL LOADINGS ON PARTIAL SYSTEM
VOLU REF QGRP NO 0 TYPE PXYS Z -5 P1 10
LC 23 $ SPECIAL LOADINGS ON PARTIAL SYSTEM
VOLU REF QGRP NO 0 TYPE PXYP Z -5 P1 10
LC 24 $ SPECIAL LOADINGS ON PARTIAL SYSTEM
VOLU REF QGRP NO 0 TYPE PXXZ Z -5 P1 10 PROJ XXL
LC 25 $ SPECIAL LOADINGS ON PARTIAL SYSTEM
VOLU REF QGRP NO 0 TYPE PXPZ Z -5 P1 10
$
END
END

+PROG ASE
HEAD silo with volume loading
ECHO FULL NO
ECHO REAC YES
LC ALLE
END

PROG WING
HEAD
LC NO 1 DESI 1
VIEW TYPE DIRE X 1 Y 0 Z 0 AXIS POSZ ROTA 0
DEFO TYPE NO EXPO 0
CTRL OPT GSTR VAL FULE
STRU NUME DSGN NUMN FULL
STRU NUME FULL FILL NO REPR DSTR UNIT DEFA SCHH YES
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
SCHH H6 0.2400000
SCH2 DIRE STAN
STRU NUME GRP NUMN QUAD FILL NO REPR DTXT UNIT DEFA SCHH YES
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
SCHH H6 -0.7400000
VIEW TYPE DIRE X 0.8648131 Y -0.4223506 Z 0.2715111 AXIS POSZ ROTA 0
DSGN TYPE SING LENG -0.8000000
STRU NUME COOZ NUMN QUAD FILL NO REPR DVEC UNIT DEFA SCHH NO
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
SCH2 DIRE STAN
LEGO TYPE FSKL Y -5
FILL TYPE RGB NUMB 5 MODE '' DMOD DDEF LIGH DEFA DARK 'A57'
FILL NUMB 40
VIEW TYPE DIRE X 0 Y -1 Z 0 AXIS POSZ ROTA 0
LOAD TYPE ALL UNIT DEFA SCHH NO SING VECT FILL AREA REPR DTXT ETYP ALL GTYP ELEM
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
LC NO 2
VIEW TYPE DIRE X 0.8688405 Y -0.3115530 Z 0.3847739 AXIS POSZ ROTA 0
LOAD TYPE ALL UNIT DEFA SCHH NO SING VECT FILL AREA REPR DTXT ETYP ALL GTYP ELEM
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
LC NO 3
VIEW TYPE DIRE X 1 Y 0 Z 0 AXIS POSZ ROTA 0
LOAD TYPE ALL UNIT DEFA SCHH NO SING VECT FILL AREA REPR DTXT ETYP ALL GTYP INP
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
LC NO 4
VIEW TYPE DIRE X 0 Y -1 Z 0 AXIS POSZ ROTA 0
LOAD TYPE ALL UNIT DEFA SCHH NO SING VECT FILL AREA REPR DTXT ETYP ALL GTYP ELEM
$
SIZ2
AND POSI 1 POSL 0 POSR 100 POSD 0 POSU 100
LC NO 5
VIEW TYPE DIRE X 1 Y 0 Z 0 AXIS POSZ ROTA 0
LOAD TYPE ALL UNIT DEFA SCHH NO SING VECT FILL AREA REPR DTXT ETYP ALL GTYP ELEM
$
END

PROG WING
$ WinGraf document (version 15.02-25) from 18.11.10 , 14:50:28
HEAD $All loads  LC:   34
LET#LF 11,12,13,14,15,21,22,23,24,25
VIEW TYPE DIRE X 0 Y -1 Z 0 AXIS POSZ ROTA 0
BOX  ZMIN 0.299903 ZMAX -1.525011 TYPE BWIN OPER AND BGRP 0
LOOP#1 LF
  LC #lf(#1)
  LOAD TYPE ALL UNIT DEFA SCHH YES SING VECT FILL AREA REPR DTXT ETYP ALL GTYP ELEM
  LOAD TYPE PP  UNIT DEFA SCHH YES SING VECT FILL NO REPR DVEC ETYP ALL GTYP LVEC
ENDLOOP
VIEW TYPE DIRE X 0 Y 0 Z -1 AXIS NEGY ROTA 0
LOOP#1 LF
  LC #lf(#1)
  LOAD TYPE ALL UNIT DEFA SCHH YES SING VECT FILL AREA REPR DTXT ETYP ALL GTYP ELEM
  LOAD TYPE PP  UNIT DEFA SCHH YES SING VECT FILL NO REPR DVEC ETYP ALL GTYP LVEC
ENDLOOP
END
