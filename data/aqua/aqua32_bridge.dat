+PROG AQUA urs:1
HEAD CROSS SECTION WITH EFFECTIVE WIDTH AND OPENINGS
$ Demonstration of variant generation possibilities
  NORM DIN EN199X-200X
  PAGE UNII 3 UNIO 3
  ECHO SECT EXTR
  CONC 1 C 25 ; STEE 2 B 500B
$
LET#PAR  0,1,2
SECT 10 TITL 'box girder'
$
  LET#PAR(0) 4.00  $ total height
  LET#PAR(1) 3.60  $ width of the box girder top
  LET#PAR(2) 3.50  $ width of the box girder below
$
  SPT  OM   0.00    0.00   MNO 0  REFS @AXIS   $ track axis
  SPT  UM   0.00   #par(0) MNO 0  REFS @BOTT   $ bottom centre
$
  SPT  OR  #par(1)  0.00   MNO 0  REFP OM      $ top right
  SPT  OL -#par(1)  0.00   MNO 0  REFP OM      $ top left
  SPT  UR  #par(2)  0.00   MNO 0  REFP UM      $ bottom right
  SPT  UL -#par(2)  0.00   MNO 0  REFP UM      $ bottom left
  SPT  UX   0.00   -0.22   MNO 0  REFP UM      $ bottom centrs
$
POLY O
VERT 10  0.00 0.00 REFP UR
     11  0.90 0.00 REFP OR +UR
     12  5.00 0.60 REFP OM +OR
     13  7.00 0.40 REFP OM +OR
     14  7.00 0.00 REFP OM +OR
     15  0.00 0.00 REFP OM ~OR
     16  7.00 0.00 REFP OM +OL
     17  7.00 -.40 REFP OM +OL
     18  5.00 -.60 REFP OM +OL
     19  0.90 0.00 REFP OL +UL
     20  0.00 0.00 REFP UL
$
POLY O MNO 0
VERT A0   0.00 0.50 REFP OM
     A1R  2.75 0.50 REFP OM +OR
     A2R  2.50 -.50 REFP UM
     A2L -2.50 -.50 REFP UM
     A1L  2.75 -.50 REFP OM +OL

CIRC 100  0.0 -0.3 0.1 MNO 0 REFP UM REFR UX
CIRC 101 +1.0 -0.3 0.1 MNO 0 REFP UM
CIRC 102 -1.0 -0.3 0.1 MNO 0 REFP UM
$
NEFF TYPE YZ YMIN  5.00 0.25 YMAX  7.50 0.25 WIDT 0.75  REFI OM RFDI +OR REFA OM RFDA +OR
NEFF TYPE YZ YMIN -5.00 0.25 YMAX -7.50 0.25 WIDT 0.75  REFI OM RFDI -OL REFA OM RFDA -OL
NEFF TYPE  Z YMIN -0.45 0.25 YMAX  0.45 0.25 WIDT 0.55  REFI OM RFDI ~OR REFA OM RFDA ~OR
$
RF  p1  Y -0.25 -0.25 REFP 10   AS 12.0 LAY 1 TORS ACTI
RF  p2  Y +0.25 -0.25 REFP 20   AS 12.0 LAY 1 TORS ACTI
$
LRF  L1 YB -0.10 -0.15 REFA 10     $$
        YE +0.10 -0.15 REFE 20     AS 1.0 LAY 1 TORS ACTI
LRF  L2 YB +4.90 +0.12 REFA OM +OR $$
        YE +0.10 -0.12 REFE OR ~OM AS 1.0 LAY 2 TORS PASS
LRF  L2 YB +0.10 -0.12 REFA OR ~OM $$
        YE +0.55 +0.12 REFE OM ~OR AS 1.0 LAY 2 TORS ACTI
LRF  L2 YB +0.55 -0.12 REFA OM ~OL $$
        YE +0.10 +0.12 REFE OL ~OM AS 1.0 LAY 2 TORS ACTI
LRF  L2 YB +0.10 +0.12 REFA OL ~OM $$
        YE +4.90 -0.12 REFE OM ~OL AS 1.0 LAY 2 TORS PASS
LRF  L3 YB -0.10 -0.15 REFA 10 $$
        YE +0.10 -0.12 REFE OR ~OM AS 1.0 LAY 3
LRF  L4 YB +0.10 -0.15 REFA 20 $$
        YE +0.10 +0.12 REFE OL ~OM AS 1.0 LAY 4
$
CRF 100  0.0 -0.3 0.12 REFP UM A 4[-] AS 1.13
CRF 101 +1.0 -0.3 0.12 REFP UM A 4[-] AS 1.13
CRF 102 -1.0 -0.3 0.12 REFP UM A 4[-] AS 1.13
$
CUT  1 ZB S
CUT  2 REFA 19 REFE 11
CUT  4 YB 2.00
CUT  5 YB 0.00
CUT 11 YB 2.0  0.5 2.0 -1.0 REFA UM REFE UM
    11 YB 2.0 -1.0 1.0 -1.0 REFA UM REFE UR
CUT 12 YB 1.0  0.5 1.0 -1.0 REFA UM REFE UM
    12 YB 1.0 -1.0 1.0 -1.0 REFA UM REFE UR
CUT 13 YB 0.0  0.5 0.0 -1.0 REFA UM REFE UM
    13 YB 0.0 -1.0 1.0 -1.0 REFA UM REFE UR
$
WPAR 0 TRAF 2.500
WPAR 1 ICE  0.200
WPAR 2 ZMAX 5.200
$
END

+PROG SOFIMSHC urs:2
HEAD AXIS IN PLAN VIEW AND ELEVATION
HEAD 3D EXTRUSION WITH CROSS FALL AND WIDENING
SYST SPAC GDIR POSZ GDIV 1000
GAX 'AXIS'
GAXA   0 0.0 0.0 SX 1.0 L 120 RA 0.0 RE 125
     160                L  40
GAX 'BOTT'
GAXA   0 0.0 0.0 SX 1.0 L 120 RA 0.0 RE 125
     160                L  40
GAXH  0.0 -3.00 250
     20.0 -3.50 250
     40.0 -5.00
     60.0 -3.50 250
     80.0 -3.00 250
    100.0 -3.50 250
    120.0 -5.00
    140.0 -3.50 250
    160.0 -3.00 250
GAXV AXIS CSA S   0 2
              S  34 2
              S  35 1
              S 125 1
              S 126 2
              S 160 2
GAXV AXIS AS0 S   0 10[cm2]
              S  35 10[cm2]
              S  40 15[cm2]
              S  50 12[cm2]
              S 110 12[cm2]
              S 120 15[cm2]
              S 125 10[cm2]
              S 160 10[cm2]
GAXV              S  V=[cm2] ID='AXIS' NAME='AS1'
                  0 11
                 35 11
                 40 15
                 50 12
                110 12
                120 15
                125 11
                160 11
GAXV AXIS AS2 S   0 10[cm2]
              S  35 10[cm2]
              S  40 15[cm2]
              S  50 12[cm2]
              S 110 12[cm2]
              S 120 15[cm2]
              S 125 10[cm2]
              S 160 10[cm2]
SPT  100 REF AX 'AXIS' SX   0.0  FIX PP
SPT  101 REF AX 'AXIS' SX  40.0  FIX PP
SPT  102 REF AX 'AXIS' SX 120.0  FIX PP
SPT  103 REF AX 'AXIS' SX 160.0  FIX PP
SLN  101 100 101 REF AXIS GRP 1 B 10 TITL 'CENTER-LINE'
SLN  102 101 102 REF AXIS GRP 1 B 10 TITL 'CENTER-LINE'
SLN  103 102 103 REF AXIS GRP 1 B 10 TITL 'CENTER-LINE'


CTRL MESH 1 ; CTRL HMIN 5.0 ; CTRL LSUP 1
END

+PROG AQUA urs:3
HEAD INTERPOLATE ALL SECTIONS
ECHO SECT
INTE ALL
END

+PROG ASE urs:4
HEAD DEAD LOAD
LC 1 DLZ 1.0
END

+PROG AQB urs:5
HEAD TEST
BEAM REF 'AXIS' CS AS CS0 "AS0" $ X 0 XE 120
BEAM REF 'AXIS'       CS0 "CSA" $ X 0 XE 120
LC 1
DESI NO SMOD NO
END

+PROG AQB urs:6
HEAD TEST
BEAM REF 'AXIS' CS AS CS0 "AS1" $ X 0 XE 120
BEAM REF 'AXIS'       CS0 "CSA" $ X 0 XE 120
LC 1
DESI NO SMOD NO
END
