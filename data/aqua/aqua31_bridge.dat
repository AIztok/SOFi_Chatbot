+PROG AQUA urs:1
HEAD CROSS SECTION WITH EFFECTIVE WIDTH AND RECESSES
HEAD TEMPLATES FOR INTERPOLATION AND INCLINED TOP DECK
$ Demonstration of variant generation possibilities
  NORM DIN EN199X-200X UNIT 3
  ECHO SECT EXTR
  CONC 1 C 25 ; STEE 2 B 500B
$
LET#PAR  0,1,2
LOOP#1 2   $ 2nd Section needed for Interpolation
SECT 11+#1 TITL 'Hollow section'
$
  LET#PAR(0) 4.00-0.70*#1  $ Total height
  LET#PAR(1) 3.60+0.80*#1  $ upper width of box
  LET#PAR(2) 3.50+0.80*#1  $ lower width of box
  LET#INCR   0.00
  LET#INCL   #PI
$
  SPT  OM   0.00     0.00   MNO 0  REFS :AXIS   $ MAIN AXIS
  SPT  UM   0.00    #par(0) MNO 0  REFS :A_UM   $ LOWER MIDDLE
$
$ VARIANT 1 based on direction om-or in reverse direction
  SPT  ORX  #par(1)  0.00   MNO 0  REFS :A_OR   $ UPPER RIGHT
  SPT  OLX -#par(1)  0.00   MNO 0  REFP OM ORX  $ UPPER LEFT
$
$ VARIANT 2 based on explicit inclinations from axis
  SPT  OR   #par(1)  0.00  MNO 0  REFP OM '=#INCR'     $ UPPER RIGHT
$ SPT  OL   #par(1)  0.00  MNO 0  REFP OM '=#INCL'     $ UPPER LEFT - POLAR DEFINITION
  SPT  OL  -#par(1)  0.00  MNO 0  REFP OM '=#INCL' >OL $ UPPER LEFT - ON TANGENT DEFINITION
$
  SPT  UR  #par(2)  0.00   MNO 0  REFP UM      $ LOWER RIGHT
  SPT  UL -#par(2)  0.00   MNO 0  REFP UM      $ LOWER LEFT
  SPT  UX   0.00   -0.22   MNO 0  REFP UM      $ LOWER CENTRE
$
POLY O
VERT 10  0.00 0.00 REFP UR
     11  0.90 0.00 REFP OR +UR
     12  5.50 0.60 REFP OM +OR
     13  7.50 0.40 REFP OM +OR
     14  7.50 0.00 REFP OM +OR
     15  0.00 0.00 REFP OM ~OR
     16  7.50 0.00 REFP OM +OL
     17  7.50 -.40 REFP OM +OL
     18  5.50 -.60 REFP OM +OL
     19  0.90 0.00 REFP OL +UL
     20  0.00 0.00 REFP UL
$
POLY O MNO 0
VERT A0   0.00 0.50 REFP OM
     A1R  2.75 0.50 REFP OM +OR
     A2R  2.50 -.50 REFP UM
     A2L -2.50 -.50 REFP UM
     A1L  2.75 -.50 REFP OM +OL
$
CIRC 100  0.0 -0.3 0.1 MNO 0 REFP UM REFR UX
CIRC 101 +1.0 -0.3 0.1 MNO 0 REFP UM
CIRC 102 -1.0 -0.3 0.1 MNO 0 REFP UM
$ VARIANT WITH VERTICAL ORIENTATION
$ NEFF TYPE YZ YMIN  7.0 ZMIN -1.0 YMAX  7.0 ZMAX 1.0  WIDT 3.0  REFI OM RFDI +OR  NO 'Y+'
$ NEFF TYPE YZ YMIN -7.5 ZMIN -1.0 YMAX -7.5 ZMAX 1.0  WIDT 4.0  REFI OM RFDI -OL  NO 'Y-'
$ NEFF TYPE  Z YMIN  0.0 ZMIN -1.0 YMAX  0.0 ZMAX 1.0  WIDT 0.9  REFI OM RFDI ~OR  NO 'MID'
$ PREFERRED VARIANT WITH HORIZONTAL ORIENTATION
NEFF TYPE YZ YMIN  5.50 0.25 YMAX  8.50 0.25 WIDT 2.00  REFI OM RFDI +OR REFA OM RFDA +OR NO 'Y+'
NEFF TYPE YZ YMIN -5.50 0.25 YMAX -8.50 0.25 WIDT 2.00  REFI OM RFDI -OL REFA OM RFDA -OL NO 'Y-'
NEFF TYPE  Z YMIN -0.45 0.25 YMAX  0.45 0.25 WIDT 0.55  REFI OM RFDI ~OR REFA OM RFDA ~OR NO 'MID'
$
RF  p1  Y -0.25 -0.25 REFP 10   AS 12.0 LAY 1 TORS ACTI
RF  p2  Y +0.25 -0.25 REFP 20   AS 12.0 LAY 1 TORS ACTI
$
LRF  L1 YB -0.10 -0.15 REFA 10     $$
        YE +0.10 -0.15 REFE 20     AS 1.0 LAY 1 TORS ACTI
LRF  L2 YB +4.90 +0.12 REFA OM +OR $$
        YE +0.10 -0.12 REFE OR ~OM AS 1.0 LAY 2 TORS PASS
LRF  L2 YB +0.10 -0.12 REFA OR ~OM $$
        YE +0.55 +0.12 REFE OM ~OR AS 1.0 LAY 2 TORS ACTI
LRF  L2 YB +0.55 -0.12 REFA OM ~OL $$
        YE +0.10 +0.12 REFE OL ~OM AS 1.0 LAY 2 TORS ACTI
LRF  L2 YB +0.10 +0.12 REFA OL ~OM $$
        YE +4.90 -0.12 REFE OM ~OL AS 1.0 LAY 2 TORS PASS
LRF  L3 YB -0.10 -0.15 REFA 10 $$
        YE +0.10 -0.12 REFE OR ~OM AS 1.0 LAY 3
LRF  L4 YB +0.10 -0.15 REFA 20 $$
        YE +0.10 +0.12 REFE OL ~OM AS 1.0 LAY 4
$
CRF 100  0.0 -0.3 0.12 REFP UM A 4[-] AS 1.13
CRF 101 +1.0 -0.3 0.12 REFP UM A 4[-] AS 1.13
CRF 102 -1.0 -0.3 0.12 REFP UM A 4[-] AS 1.13
$
CUT  1 ZB S VYFK 0
CUT  2 REFA 19 REFE 11 VYFK 0
CUT  3 ZB 3.00  VYFK 0
CUT  4 YB 2.00
CUT  5 YB 0.00  VZFK 0

CUT 11 YB 1.2  0.5 1.2 -1.2 REFA UM REFE UM
    11 YB 1.2 -1.2 1.0 -1.2 REFA UM REFE UR
CUT 12 YB 1.0  0.5 1.0 -1.0 REFA UM REFE UM
    12 YB 1.0 -1.0 1.0 -1.0 REFA UM REFE UR
CUT 13 YB 0.0  0.5 0.0 -1.0 REFA UM REFE UM
    13 YB 0.0 -1.0 1.0 -1.0 REFA UM REFE UR
$
WPAR 0 TRAF 2.500
WPAR 1 ICE  0.200
WPAR 2 ZMAX 5.200
$
ENDLOOP
$
$ INTERPOLATION WITH FACTOR
  INTE 13 11  12 0.5
  INTE 14 11 -12 0.5
$
END


+PROG RESULTS urs:2
head
SIZE SPLI 4x1
loop#qnr 4
LC SECT 11+#qnr; STRU CROS REPR DSTR
LC SECT 11+#qnr; CROS NAME REPR DTXT
endloop
END

+PROG SOFIMSHC urs:3
HEAD AXIS IN PLAN VIEW AND ELEVATION
HEAD 3D EXTRUSION WITH INCLINATION AND CHANGE OF WIDTH
SYST SPAC GDIR POSZ GDIV 1000
GAX 'AXIS' TYPE AXIS
   GAXA  S  0.0 X 0.00 SX 1.0 L 80.0 RA 75
   GAXP  S  0.0 ALF 10 INCR -0.02 +0.02
   GAXP  S 80.0 ALF  0 INCR +0.04 -0.02
GAX 'A_OR' TYPE BGEO
   GAXA   0.0  Y 3.60 SX 1.0 L 80.0 RA 71
GAX  'A_UM' TYPE BGEO
   GAXA   0.0  SX 1.0 L 80.0 RA 75
   GAXH   0.0  -8.0
         40.0  -5.0 R 1600
         80.0  -4.0
$
SPT 100  0.0  0.00  0.00 ; 110  65.00 37.50  0.00
SLN 100 100 110 REF AXIS GRP 1 'B' 11 TITL 'CENTER-LINE'
CTRL MESH 1 ; CTRL HMIN 10.0 ; CTRL LSUP 1
END

+PROG AQUA urs:4
HEAD EXPLICITE INTERPOLATE ALONG THE AXIS
ECHO SECT
INTE 101 11 S  0 NREF 100
INTE 102 11 S 20 NREF 100
INTE 103 11 S 40 NREF 100
INTE 104 11 S 60 NREF 100
INTE 105 11 S 80 NREF 100
END

+PROG AQUA urs:5
HEAD INTERPOLATE ALL SECTIONS
ECHO SECT
INTE ALL NREF 100
END

+PROG RESULTS urs:6
head
SIZE SPLI 2x1
loop#qnr 1
LC SECT 101+#qnr; STRU CROS REPR DSTR
LC SECT 101+#qnr; CROS NAME REPR DTXT
endloop
END

