+PROG AQUA urs:1
HEAD TEMPLATE SECTION WITH VARIABLES ALONG AN AXIS
$
  NORM DIN EN199X-200X
  ECHO SECT EXTR
  CONC 1 C 30 ; STEE 2 B 500B
$
SECT 10 TITL 'Hollow section'
$
  TVAR HEIGHT 4.0[m]  CMNT 'Total height'
  TVAR WIDTHU 3.6[m]  CMNT 'upper half width of box'
  TVAR WIDTHL 3.5[m]  CMNT 'lower half width of box'
  TVAR INC    0.00    CMNT 'INCLINATION AS TANGENS'
  TVAR ALFR    0.0    CMNT 'INCLINATION ID TO THE RIGHT'
  TVAR ALFL    #PI    CMNT 'INCLINATION ID TO THE LEFT'
$
  SPT  OM   0.00      0.00    MNO 0  $ AXIS OF BRIDGE
  SPT  UM   0.00   '=#HEIGHT' MNO 0  $ LOWER MIDDLE
$
  SPT  OR '=+#WIDTHU'  '=+#WIDTHU*#INC'  MNO 0  $ UPPER RIGHT
  SPT  OL '=-#WIDTHU'  '=-#WIDTHU*#INC'  MNO 0  $ UPPER LEFT
  SPT  UR '=+#WIDTHL'  '=#HEIGHT'        MNO 0  $ LOWER RIGHT
  SPT  UL '=-#WIDTHL'  '=#HEIGHT'        MNO 0  $ LOWER LEFT
  SPT  UX      0.0       -220.0          MNO 0  REFP UM  $ LOWER CENTER
  SPT  OXR  2000.0          0.0          MNO 0  REFP OM '=#ALFR' $ UPPER DECK RIGHT
  SPT  OXL  2000.0          0.0          MNO 0  REFP OM '=#ALFL' $ UPPER DECK LEFT
                                             $  REFP OM '=180+#ALF' MAX 8 CHARS !
$
POLY O
VERT 10     0    0 REFP UR
     11   900    0 REFP OR +UR
     12  5000  600 REFP OM +OR
     13  7000  400 REFP OM +OR
     14  7000    0 REFP OM +OR
     15     0    0 REFP OM ~OR
     16  7000    0 REFP OM +OL
     17  7000 -400 REFP OM +OL
     18  5000 -600 REFP OM +OL
     19   900    0 REFP OL +UL
     20     0    0 REFP UL
$
POLY O MNO 0
VERT A0      0  500 REFP OM
     A1R  2750  500 REFP OM +OR
     A2R  -750 -500 REFP UR
     A2L   750 -500 REFP UL
     A1L  2750 -500 REFP OM +OL
$
CIRC 100     0  -300 100 MNO 0 REFP UM REFR UX
CIRC 101 +1000  -300 100 MNO 0 REFP UM
CIRC 102 -1000  -300 100 MNO 0 REFP UM
$
NEFF TYPE YZ YMIN  5000  250 YMAX  7500  250 WIDT 1000  REFI OM RFDI +OR REFA OM RFDA +OR  NO 'Y+'
NEFF TYPE YZ YMIN -5000  250 YMAX -7500  250 WIDT 1000  REFI OM RFDI -OL REFA OM RFDA -OL  NO 'Y-'
NEFF TYPE  Z YMIN  -450  250 YMAX   450  250 WIDT 0550  REFI OM RFDI ~OR REFA OM RFDA ~OR  NO 'MID'
$
RF  p1  Y  -250  -250 REFP 10   AS 12.0 LAY 1 TORS ACTI
RF  p2  Y  +250  -250 REFP 20   AS 12.0 LAY 1 TORS ACTI
$
LRF  L1 YB  -100  -150 REFA 10     $$
        YE  +100  -150 REFE 20     AS 1.0 LAY 1 TORS ACTI
LRF  L2 YB +4900  +120 REFA OM +OR $$
        YE  +100  -120 REFE OR ~OM AS 1.0 LAY 2 TORS PASS
LRF  L2 YB  +100  -120 REFA OR ~OM $$
        YE  +550  +120 REFE OM ~OR AS 1.0 LAY 2 TORS ACTI
LRF  L2 YB  +550  -120 REFA OM ~OL $$
        YE  +100  +120 REFE OL ~OM AS 1.0 LAY 2 TORS ACTI
LRF  L2 YB  +100  +120 REFA OL ~OM $$
        YE +4900  -120 REFE OM ~OL AS 1.0 LAY 2 TORS PASS
LRF  L3 YB  -100  -150 REFA 10 $$
        YE  +100  -120 REFE OR ~OM AS 1.0 LAY 3
LRF  L4 YB  +100  -150 REFA 20 $$
        YE  +100  +120 REFE OL ~OM AS 1.0 LAY 4
$
CRF K0     0  -300 120 REFP UM A 4[-] AS 1.13
CRF K1 +1000  -300 120 REFP UM A 4[-] AS 1.13
CRF K2 -1000  -300 120 REFP UM A 4[-] AS 1.13
$
CUT  1 ZB S  VYFK 0
CUT  2 REFA 19 REFE 11  VYFK 0
CUT  4 YB 1500
CUT  5 YB    0  VZFK 0
CUT 11 YB 1500   500 1500 -1000 REFA UM REFE UM
    11 YB 1500 -1000 1000 -1000 REFA UM REFE UR
CUT 12 YB 1000   500 1000 -1000 REFA UM REFE UM
    12 YB 1000 -1000 1000 -1000 REFA UM REFE UR
CUT 13 YB  100   500  100 -1000 REFA UM REFE UM
    13 YB  100 -1000 1000 -1000 REFA UM REFE UR
$
END

+PROG SOFIMSHC urs:2
HEAD 3D EXTRUSION SYSTEM BY TEMPLATES
SYST SPAC GDIR POSZ GDIV 1000
ECHO GEOM

$ PREDEFINE LOCAL VARIBALES TO PASS THE CHECKS
LET#S_ACT  0.00  $ DISTANCE ALONG THE BRIDGE
LET#INC    0.00  $ INCLINATION AS TANGENS
LET#ALFR    0.0  $ INCLINATION ID TO THE RIGHT
LET#ALFL    #PI  $ INCLINATION ID TO THE LEFT

GAX  'AXIS'
GAXA    0.0  X    0.00   0.00  SX 1.0
       40.0  L 40     LA -125
      120.0  L 80     LA -125
      160.0  L 40
GAXV 'AXIS' HEIGHT   0.0 3.50[m] TYPE D+
     'AXIS' HEIGHT  40.0 6.00[m]
     'AXIS' HEIGHT  60.0 4.50[m] TYPE D*
     'AXIS' HEIGHT 100.0 4.50[m] TYPE D*
     'AXIS' HEIGHT 120.0 6.00[m]
     'AXIS' HEIGHT 160.0 4.00[m] TYPE D-
GAXV 'AXIS' WIDTHL   0.0 '=MIN(3500,2500+(80-#S_ACT)**2/1.6)' TYPE FUNC
GAXV 'AXIS' INC      0.0 0.00
     'AXIS' INC    120.0 0.02
GAXV 'AXIS' ALFR     0.0 '=ARC(ATN(+#INC,+1))' TYPE FUNC
GAXV 'AXIS' ALFL     0.0 '=ARC(ATN(-#INC,-1))' TYPE FUNC
$
SPT  100 REF AX 'AXIS' SX   0.0 FIX PPMM
SPT  101 REF AX 'AXIS' SX  40.0 FIX PP
SPT  102 REF AX 'AXIS' SX 120.0 FIX PP
SPT  103 REF AX 'AXIS' SX 160.0 FIX PP
SLN  101 100 101 REF AXIS GRP 1 B 10 TITL 'CENTER-LINE'
SLN  102 101 102 REF AXIS GRP 1 B 10 TITL 'CENTER-LINE'
SLN  103 102 103 REF AXIS GRP 1 B 10 TITL 'CENTER-LINE'
CTRL MESH 1 ; CTRL HMIN 5.0 ; CTRL LSUP 1
END

+PROG AQUA urs:3
HEAD INTERPOLATE ALL SECTIONS
ECHO SECT
INTE ALL
END
