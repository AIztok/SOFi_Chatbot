!#!Info Example:    Design US AASHTO-2004
!#!Info Program:    BEMESS
!#!Info Keyword:    Static Analysis, Design
!#!Info Date:       06/21/2007

!#!KAPITEL Material and System generation
+PROG AQUA URS:1
HEAD US AASHTO-2004
UNIT 0 ; ECHO MAT FULL
NORM US AASHTO-2004
CONC 1 ACI
STEE 3 SACI
SREC 1 H .60[m] B .30[m] HO .25[m] BO 1.20[m]
END

+PROG SOFIMSHA urs:2
HEAD
UNIT 0 ; SYST GIRD GDIV 10000 POSZ ; ECHO FULL NO ; ECHO SYST YES
NODE  NO     X     Y
       1    0.0   0.0
       2    0.0   4.5
       3    0.0   9.0
       4    0.0  13.5
NODE MESH  1 2 DIV 9
NODE MESH  2 3 DIV 9
NODE MESH  3 4 DIV 9
NODE FIT   1 4
TRAN NODE 1 4  FIT DX  6.0  DNO 10
NODE FIT   1 3
TRAN NODE 1 3  FIT DX 12.0  DNO 20
$
NODE MESH  1 11 DIV 12
NODE MESH 11 21 DIV 12
$
GRP 1
  QUAD FIT 1 2 22 21 T 0.25[m] MNO 1  MRF 3
GRP 2
  QUAD FIT 2 3 23 22 T 0.25[m] MNO 1  MRF 3
GRP 3
  QUAD FIT 3 4 14 13 T 0.25[m] MNO 1  MRF 3
$
GRP 0
  BEAM FIT 21 23 NCS 1
$
NODE  1,2,11,12,21,22,23 FIX PP
$
GRP 3
  BOUN 1 TITL 'Wall 1' ;   BOUN  3  4 FIT TYPE CZ CA 1000000 REF YY
  BOUN 2 TITL 'Wall 2' ;   BOUN 13 14 FIT TYPE CZ CA 1000000 REF YY
  BOUN 3 TITL 'Wall 3' ;   BOUN 13 23 FIT TYPE CZ CA 1000000 REF XX
  BOUN 4 TITL 'Wall 4' ;   BOUN  4 14 FIT TYPE CZ CA 1000000 REF XX
END


+PROG SOFILOAD urs:8
head
ACT D_1
ACT LL
LC 1 FACD 1 TYPE D_1
  quad GRP 1,2,3 TYPE PZZ P 4.5
LC 2       TYPE LL
  quad GRP 1 TYPE PZZ P 3.5
LC 3       TYPE LL
  quad GRP 2 TYPE PZZ P 3.5
LC 4       TYPE LL
  quad GRP 3 TYPE PZZ P 3.5
end

!#!KAPITEL Analysis
+PROG ASE URS:3
HEAD
LC ALL
END

PROG MAXIMA URS:5
HEAD US AASHTO-2004
HEAD Superposition ULS - Ultimate Limit State
ECHO FULL NO ; ECHO TABS YES
$
  COMB 1 EXTR EXPL TYPE DESI BASE 2100   $ superposition for design LC 2100...
  ADD D_1 FACU 1.25 FACF 0.9
  ADD LL  FACU 1.75 FACF 0.0
  SUPP 1 EXTR MAMI ETYP QUAD TYPE M     TITL 'ULS_QUAD'
  SUPP 1 EXTR MAMI ETYP QNOD TYPE M     TITL 'ULS_QUAD'
  SUPP 1 EXTR MAMI ETYP QUAD TYPE M     TITL 'ULS_QUAD'
  SUPP 1 EXTR MAMI ETYP QNOD TYPE VX,VY TITL 'ULS_QUAD'
  SUPP 1 EXTR MAMI ETYP NODE TYPE PZ    TITL 'ULS_NODE'
  $SUPP 1 EXTR MAMI ETYP QBED TYPE P     TITL 'ULS_QUAD' $ QUAD-bedding, if available
  SUPP 1 EXTR MAMI ETYP BEAM TYPE MY    TITL 'ULS_BEAM'
END

+PROG BEMESS URS:6
HEAD Definitions of the Design Parameters see bemess6_design.dat
GEOM - HA 25[mm] DHA 10[mm] HB 25[mm] DHB 10[mm] $ mm
DIRE  0  0
PUNC D 0.40[m] B 0.40[m] $ manual column dimensions for punching
                   $ for graphic input not necessary here!
PARA DU 10[mm]
END

+PROG BEMESS URS:7
HEAD US AASHTO-2004
ECHO PUNC FULL
CTRL ULTI ; LC DESI
END

-PROG AQB URS:4
HEAD US AASHTO-2004
LC TYPE (D)       $ design load cases
DESI STAT ULTI
END

$ Clean file folder:
+sys del $(project).$d?


