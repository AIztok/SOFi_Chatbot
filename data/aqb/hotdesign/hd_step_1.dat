!#!Info  Example:       HOTDESIGN - Part 1 : Thermal analysis
!#!Info  Keyword:       fire; hot design
!#!Info  Program:       HYDRA
!#!Info  Date:          $Date: 2015-07-27 23:26:47 +0200 (Mo, 27 Jul 2015) $

PROG AQUA
HEAD Calibration of temperature distribution of a column (BK 2003 S. 211)
NORM DIN 1045-1
ECHO MAT
CONC 1,2 C 30 RHO 2300
STEE 11  BST 500SA
TIMB 21  C   27
HMAT NO TYPE  NSP  TITL
      1 FOUR  0.05 CONCRETE
HMAT NO TYPE  TEMP  NSP  TITL
      2 FOUR    0   0.05 CONCRETE_EXPLICIT
      2 FOUR   20,100,115,200,400,600,800,1000  =
HMAT NO TYPE  NSP  TITL
     11 FOUR  0.05 REINFORCEMENTS
HMAT NO TYPE  NSP  TITL
     21 FOUR  0.05 TIMBER_MATERIAL
END

PROG SOFIMSHA
HEAD Calibration of temperature distribution of a column (BK 2003 S. 211)
SYST SECT GDIV 10000
ECHO FULL YES
NODE NO Y Z
 700  0.20 -0.225 ; 712 -0.20 -0.225
 100  0.20  0.225 ; 112 -0.20  0.225
GRP 0
QUAD IMSH 100 112 712 700 M 12 12 MNO 1
END

PROG HYDRA
HEAD FIRE LOADING ON FE-SECTION
SYST DIMT MIN DIMQ KW ; ECHO VELO,QUAN NO
$ ECCS 1981 / ISO-834 resp. ETK DIN 4102-2
LET#N 90
LOOP#1 3
  STEP #N 1.0 HOPT 20 ; PLC HP 20.0 ; CTRL KITE 0
  LET#LF 1+#1
$ CONDUCTIVITY IN KW/M/K = 0.001-times !!!
  IF #LF==1
     LC #LF TITL 'Interior column'
        BOUN SPEC 0.040  100  112  1 VP 1.0 EPS 0.40 F #LF $ hot surface
             SPEC 0.040  112  712 50 ==
             SPEC 0.040  700  712  1 ==
             SPEC 0.040  100  700 50 ==
  ENDIF
  IF #LF==2
     LC #LF TITL 'Column at egde'
        BOUN SPEC 0.040  100  112  1 VP 1.0 EPS 0.40 F #LF $ hot surface
             SPEC 0.040  112  712 50 ==
             SPEC 0.040  100  700 50 ==
             SPEC 0.040  700  712  1 VP 20
  ENDIF
  IF #LF==3
     LC #LF TITL 'Column at corner'
        BOUN SPEC 0.040  100  112  1 VP 1.0 EPS 0.40 F #LF $ hot surface
             SPEC 0.040  112  712 50 ==
             SPEC 0.040  700  712  1 VP 20
             SPEC 0.040  100  700 50 ==
  ENDIF
  FUNC 180 ETK #LF
  END
ENDLOOP
END

PROG WING
HEAD ISO FIRE DESIGN TEMPERATURES
SIZE -4
VIEW STAN X +1.0 AXIS POSZ
STRU NUME MNO NUMN 0 ; FILL COLO
LOOP#1 3 $ loadcases
LOOP#2 3 $ temperatures
   LC #1+1 (#2+1)*30*60
   ISOL TEMP 0 1200 100 ; AND ; STRU CONT
   ISOL TEMP 0 1000 100 FILL YES ; AND ; STRU CONT
ENDLOOP
ENDLOOP
END
END
