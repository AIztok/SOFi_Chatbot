!#!Info  Example:       HOTDESIGN - Part 2 : Simplified structural analysis
!#!Info  Keyword:       fire; hot design
!#!Info  Program:       AQB
!#!Info  Date:          $Date: 2015-08-01 19:08:53 +0200 (Sa, 01 Aug 2015) $

+PROG AQUA
HEAD HOT DESIGN OF COLUMN WITH MANUAL SUBDIVISION
NORM DIN 1045-1 UNIT 5
ECHO MAT  FULL
ECHO SECT FULL
$
$ DEFINE MATERIAL FOR DISCRETE TEMPERATURE LEVELS
LET#TEMP  20,200,400,600,800
CONC 1 C 30 $ ALL IN ONE FOR GRAFICS ONLY
LOOP#1 5 ; SSLA  ULTI TEMP  #TEMP(#1) ; ENDLOOP
STEE 2 BST 500SA $ ALL IN ONE FOR GRAFICS ONLY
LOOP#1 5 ; SSLA ULTI TEMP #TEMP(#1) ; ENDLOOP
$
$ SEPARATE MATERIALS FOR EXPLICIT SECTION WITH SHIFT
$
LOOP#1 5 ; LET#3 #TEMP(#1)
  CONC 10+#1 C 30 TITL '(#3 Grad)'
  SSLA ULTI 1.3 TEMP #3 EPSS SHIF
ENDLOOP
$
STEE 20 BST 500SA $ TEMPERATURE REDUCTION AUTOMATICALLY
$
CTRL SCUT 0 ; CTRL STYP 0 $ NO SHEAR + TORSION
$
LET#N  5                              $ NUMBER OF LAYERS
LET#M  14,13,12,11,10                 $ MATERIAL NUMBERS
LET#B  200,188,176,160,118  $ WIDTH OF REGIONS
LET#H  225,214,200,187,143  $ HEIGHT OF REGIONS
LET#D   20
LET#AS  0.7854E-2*#D**2 $ AREA OF BAR
LET#BAS 160
LET#HAS 185
LET#TEMPAS 500,400
$
SECT 1 MNO 10 TITL 'Interior column'
LOOP#1 #N
   POLY O MNO #M(#1) ; VERT '#1-1' -#B(#1) -#H(#1)
                            '#1-2' +#B(#1) -#H(#1)
                            '#1-3' +#B(#1) +#H(#1)
                            '#1-4' -#B(#1) +#H(#1)
ENDLOOP
RF  OL +#BAS   -#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  OM +#BAS/3 -#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  OM -#BAS/3 -#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  OR -#BAS   -#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  UL +#BAS   +#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  UM +#BAS/3 +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UM -#BAS/3 +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UR -#BAS   +#HAS   #AS D #D MRF 20 TEMP #TEMPAS
$
SECT 2 MNO 10 TITL 'Column at egde'
LOOP#1 #N
   POLY O MNO #M(#1) ; VERT '#1+1' -#B(#1) -#H(#1)
                            '#1-2' +#B     -#H(#1)
                            '#1-3' +#B     +#H(#1)
                            '#1-4' -#B(#1) +#H(#1)
ENDLOOP
RF  OL +#BAS   -#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  OM +#BAS/3 -#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  OM -#BAS/3 -#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  OR -#BAS   -#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UL +#BAS   +#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  UM +#BAS/3 +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UM -#BAS/3 +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UR -#BAS   +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
$
SECT 3 MNO 10 TITL 'Column at corner'
LOOP#1 #N
   POLY O MNO #M(#1) ; VERT '#1+1' -#B(#1) -#H
                            '#1-2' +#B     -#H
                            '#1-3' +#B     +#H(#1)
                            '#1-4' -#B(#1) +#H(#1)
ENDLOOP
RF  OL +#BAS   -#HAS   #AS D #D MRF 20
RF  OM +#BAS/3 -#HAS   #AS D #D MRF 20
RF  OM -#BAS/3 -#HAS   #AS D #D MRF 20
RF  OR -#BAS   -#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  UL +#BAS   +#HAS   #AS D #D MRF 20 TEMP #TEMPAS
RF  UM +#BAS/3 +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UM -#BAS/3 +#HAS   #AS D #D MRF 20 TEMP #TEMPAS(1)
RF  UR -#BAS   +#HAS   #AS D #D MRF 20 TEMP #TEMPAS
END

+PROG AQB
HEAD Eigen stresses caused by temperatures in the sectional plane
ECHO NSTR
S 1 1 X 0 N   0.0
  1 1 X 1 N -431.
  1 1 X 2 N -831.
S 2 2 X 0 N   0.0
  2 2 X 1 N -431.
  2 2 X 2 N -831.
S 3 3 X 0 N   0.0
  3 3 X 1 N -431.
  3 3 X 2 N -831.
NSTR ULTI KSV UL UL
END

+PROG SOFIMSHA
HEAD Reference example 9B for DIN 1045-1
SYST SPAC GDIV 1000 GDIR NEGX
NODE 1 FIX PPMM
     2 X 6.2
GRP 1 ; BEAM 1 1 2 NCS 1 DIV 6
GRP 2 ; BEAM 1 1 2 NCS 2 DIV 6
GRP 3 ; BEAM 1 1 2 NCS 3 DIV 6
END

+PROG SOFILOAD
HEAD ENV 1992-1-2: Reinforced column according to the exact method
$ ECHO STEP 1
LOOP#1 3
LC #1+1
NODE 2 PXX -431 $ Loads including partial safety factors
BEPL 1000*(#1+1)+1 TYPE UY 4.4[mm] A 1.0 REF SS  $ imperfection
BEPL 1000*(#1+1)+1 TYPE UZ 4.4[mm] A 1.0 REF SS  $ imperfection
ENDLOOP
END

+PROG STAR2
HEAD ENV 1992-1-2: Reinforced column according to the zonal method
ECHO STEP 1
LOOP#1 1
CTRL II 50 $ 2nd order theory
GRP #1+1 ; LC #1+1 $ STEU INTE 4
DESI ACCI SC1 1.3 KSV ul  KSB ul
NSTR AK1          KSV ul  KSB ul FMAX 4.0
ECHO NSTR
END
ENDLOOP
END
