PROG AQUA
HEAD BETONKALENDER PRESTRESSED CONCRETE GIRDER KUPFER (DIN 1045-1 variant)
NORM DIN EN199X-200X $ Prestress pretensioned XC1
ECHO MAT ; ECHO SECT EXTR
CONC 1 C 45 ; STEE 11 S 500 ; 12 Y 1770C
SECT 1 1 11 ; POLY OPZ
VERT 10 50   0
     12 50  35
     14 20  60
     16 20 175
     18 60 185
     20 60 220
CUT S1 ZB  60
    S2 ZB 115
    S3 ZB 175
   S11 YB  20  ZB   70 ZE   0  BMAX 999 TYPE FLAN LAY 2
   S12 YB  20  ZB  230 ZE 170  BMAX 999 TYPE FLAN LAY 3
SPT 'O' Z   0
SPT 'U' Z 220
$ Minimum reinforcements disturb the stress range because ETA will become quite large
LRF 1 50 210 -50 210 AS 0.1 D 6 LAY 1
LRF 2 40  10 -40  10 AS 0.1 D 6 LAY 2
END

PROG SOFIMSHA
HEAD BETONKALENDER PRESTRESSED CONCRETE GIRDER KUPFER
HEAD STATIC SYSTEM OF THE SINGLE SPAN GIRDER
$ Support width 5 cm , determinant shear section (A+D)/2 =13.5
$ transmission length 40 cm
SYST FRAM
NODE 1 0.0 FIX PP ; 2 3.88 FIX PY
BEAM 101 1 2 NCS 1
STO#X 0.000,0.135,0.400,0.913,1.427,1.940,2.453,2.967,3.480,3.745,3.880
LOOP#1 9 ; BSEC X #X(#1+1) ; ENDLOOP
END

PROG AQB
HEAD DEFINITION OF THE TENDONS
$ support point, 15.8 percent prestress
$ determinant shear section, 44.2 percent prestress
$ end of the transmission length, 100 percent prestress
  TEND 101 10 12 0 0 0 X #X(0)    Z 202 ZZ 117.6*0.158 120
  TEND 101 20 12 0 0 0 X #X(0)    Z  18 ZZ  12.0*0.158  40
  TEND 101 10 12 0 0 0 X #X(1)    Z 202 ZZ 117.6*0.442 120
  TEND 101 20 12 0 0 0 X #X(1)    Z  18 ZZ  12.0*0.442  40
LOOP#1 7
  TEND 101 10 12 0 0 0 X #X(#1+2) Z 202 ZZ 117.6       120
  TEND 101 20 12 0 0 0 X #X(#1+2) Z  18 ZZ  12.0        40
ENDLOOP
  TEND 101 10 12 0 0 0 X #X(9)    Z 202 ZZ 117.6*0.442 120
  TEND 101 20 12 0 0 0 X #X(9)    Z  18 ZZ  12.0*0.442  40
  TEND 101 10 12 0 0 0 X #X(10)   Z 202 ZZ 117.6*0.158 120
  TEND 101 20 12 0 0 0 X #X(10)   Z  18 ZZ  12.0*0.158  40
END

PROG SOFILOAD
HEAD LOADING OF THE SINGLE SPAN GIRDER
LET#1 8/3.88**2
ACT 'G_1' ;  LC 1 TITL 'Self weight' ; BEAM 101 TYPE PYY 0.7*#1
ACT 'G_2' ;  LC 2 TITL 'Add. weight' ; BEAM 101 TYPE PYY 5.3*#1
ACT 'Q_A' ;  LC 3 TITL 'Live Load'   ; BEAM 101 TYPE PYY 7.0*#1
ACT 'P'   ;  LC 4 TITL 'Prestress'   ; BEPL 101 TYPE PRE OPT 0
END
END

PROG STAR2
HEAD STATIC SYSTEM OF THE SINGLE SPAN GIRDER
CTRL I ; LC 1,2,3,4
END
END

PROG AQB
HEAD CREEP REDISTRIBUTION IN PRECASTING WORKS
BEAM 101 CS0 1
LC 1,4 ; LC 5 C
COMB SUM LC1 G_1 LC2 P LCST 5
EIGE 1 RH 70 T0 3 T 180
COMB SUM LC1 G_1 LC2 P
STRE E BB
END
HEAD CREEP REDISTRIBUTION TILL INFINITE
ECHO EIGE EXTR '?0'
BEAM 101 CS0 1 CS1 1
LC 1,2,4 ; LC 5,6 C
COMB SUM LC1 G LC2 P LC3 C LCST 6
EIGE 1 RH 50
END
END

PROG AQB
HEAD Checks
ECHO COMB
$
BEAM 101 CS0 1 CS1 1
$ BEAM 101 BA0 1 BA1 1 LAMS -1.0 LAMT -1.0 LAMC -1.0 LAML -1.0 $ Simplified
$
LC 1,2,3,4 ; LC 5,6 C
COMB SOLO LC1 P
COMB SOLO LC1 G_1
COMB SOLO LC1 G_2
COMB SOLO LC1 Q
COMB SOLO LC1 5
COMB SOLO LC1 6
COMB SUM  LC1 G_1 1.0 P 1.0 C 1.0
COMB SUM  LC1 G_1 1.0 G_2 1.0 P 1.0 Q 1.0 C 1.0
STRE E BH
$
COMB SUMF LC1 G_1 - G_2 - Q - P - C - LCST 101
STRE E RL $ minimum Reinforement 4.4.2.2.
$ Check of ultimate limit state
COMB SUMD LC1 G_1 - G_2 - Q - P - C - LCST 102
DESI STAT ULTI
$ Check of crack width category D + Stress range
COMB GMAX LCST 103
COMB MAMF LC1 G_1 - G_2 - Q - P 0.9 C 0.9
COMB MAMF LC1 G_1 - G_2 - Q - P 1.1 C 1.1
NSTR SERV CW 0.2 CHKC -0.6 CHKS 0.80 CHKT 1.0 FAT DINF
END
END
