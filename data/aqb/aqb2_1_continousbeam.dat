PROG AQUA
HEAD TWO-SPAN CONTINUOUS BEAM WITH CANTILEVER ARM  ACCORDING TO EC
$ CONTINUOS BEAM IN INDUSTRIAL CONSTRUCTION
PAGE UNII 1
NORM DIN EN199X-200X
CONC 1 C  20  $ DIN 1045 7.4.5.3 25/1.18 = 21.19
STEE 2 S 450B FY 400 $ OLDER STEEL GRADE BST 420
SREC 1  100   35 15 210 70[mm] 70[mm] MRF 2 DASO 20 DASU 25
SREC 2  150   35 15 210  ==
SREC 3  150   35 15 284  ==
END

+PROG SOFIMSHA
HEAD Two span continuous girder with cantilever
$ System
SYST FRAM GDIR POSY
NODE 1  0.00
     2  4.00 FIX PP
     3 12.00 FIX PP
     4 20.00 FIX PP
BEAM 101 1 2 1.2 DIV 4
     102 2 3 2   DIV 8
     103 3 4 3   DIV 8
BSEC NO   X    TYPM  SPEC
     101 0.94  SHEA  E
     101 0.20  FACE  E
BSEC NO   X   NCS  TYPM  SPEC
     102 0.20   2  FACE  A
     102 0.95   2  SHEA  A
     102 0.95   2  SHEA  E
     102 0.20   2  FACE  E
BSEC NO   X   NCS  TYPM  SPEC
     103 0.20   3  FACE  A
     103 0.95   3  SHEA  A
     103 0.25   3  IFAC  E
END

PROG AQUA
HEAD INTERPOLATION
INTE 0
END

PROG SOFILOAD
HEAD ACTIONS AND LOADINGS
ACT G
ACT Q
LC 1 G ; BEPL 101     TYPE PYY 214.00        A  0.2
         BEAM 101     TYPE PYY  11.00 90.00
         BEPL 102     TYPE PYY 416.00        A  4.0
         BEAM 102 103 TYPE PYY  90.00
LC 2 Q ; BEAM 101     TYPE PYY  40.00
LC 3 Q ; BEAM 102     TYPE PYY  40.00
         BEPL 102     TYPE PYY 284.00        A  4.0
LC 4 Q ; BEAM 103     TYPE PYY  30.00
END

PROG STAR2
HEAD TWO-SPAN CONTINUOUS BEAM WITH CANTILEVER ARM
$ CONTINUOS BEAM IN INDUSTRIAL CONSTRUCTION
CTRL I ; LC 1,2,3,4
END
END


PROG MAXIMA
HEAD SUPERPOSITION ULTIMATE DESIGN
COMB 1 DESI
ACT G
  LC 1
ACT Q
  LC 2,3,4
SUPP 1 EXTR MAMI ETYP BEAM TYPE MY LC 111
SUPP 1 EXTR MAMI ETYP BEAM TYPE VZ LC 113
END

PROG AQB
HEAD TWO-SPAN CONTINUOUS BEAM WITH CANTILEVER ARM
$ CONTINUOS BEAM IN INDUSTRIAL CONSTRUCTION
CTRL VM 'STD'
LC 111,112,113,114
DESI ULTI
END
END

PROG MAXIMA
HEAD SUPERPOSITION LIFE LOAD
COMB 1 STAN
LC 2,3,4 Q
SUPP 1 EXTR MAMI ETYP BEAM TYPE MY LC 105
END

PROG AQB
HEAD DESIGN ACCORDING EC2 AND DESIGN COMBINATION
LC 1   G
   105 L
   106 L
$
COMB MAMI MY LC1 G 1.35 L 1.50
DESI ULTI
$
COMB MAMI MY LC1 G 1.00 L 1.00
NSTR S0 KSV SL CW 0.3
END
END



