+PROG AQUA
$AQB-MANUAL
HEAD EXAMPLE  PREFABRICATED COLUMN ACOORDING TO DIFFERENT DESIGN CODES
$THERE ARE SLIGHT DEVIATIONS, DUE TO THE NEW PERIPHERIC REINFORCEMENT
$DISTRIBUTION SYM WHICH IS IN STRICT ACCORDANCE WITH THE DESIGN CODES
NORM DIN EN199X-200X ; ECHO MAT
TVAR ALF-CE 0.85 SCOP DESI
CONC 11 C 35   ; SSLA CALC -1.30
STEE 12 B 500B ; SSLA CALC -1.30
SREC 1 400[mm] 400[mm] RTYP CORN MNO 11 MRF 12
$
END

PROG SOFIMSHA
HEAD EXAMPLE  PREFABRICATED COLUMN ACOORDING TO DIFFERENT DESIGN CODES
SYST FRAM GDIR NEGX GDIV 1000
CTRL CONT 31 $ SUBDIVIDE COLUMN IN TWO BEAMS
NODE 1  0 FIX F
     2  6 FIX PY
     3 12
GRP 1
BEAM 1 1 2 1 DIV 4
     2 2 3 1 DIV 4
END

PROG SOFILOAD
HEAD LOADS
LC 11 ; NODE 2 TYPE PX -300. ; 3 == ; 3 TYPE PY 20.
END

PROG STAR2
HEAD TOTAL FACTURED ULTIMATE LOADS
CTRL I ; LC 11 1.50
END

PROG AQB
HEAD STANDARD DESIGN
LC 11
REIN MOD BEAM SAVE ; DESI ULTI SMOD NO
END

PROG STAR2
HEAD DESIGN AND NONLINEAR STIFFNESS FOR ITERATION
CTRL II 20
LC 12 ; LCC 11 1.50
REIN MOD BEAM ; DESI ULTI S2 5.0 SMOD NO
NSTR S1 SLD SLD ALPH 0.4
END

PROG STAR2
HEAD GENERAL NONLINEAR ANALYSIS
CTRL II 20 $ ECHO STEP 1
LC 13 ; LCC 11 1.50
REIN MOD BEAM ; DESI NONL S2 5.0 SMOD NO
NSTR S1 CALD CALD ALPH 0.4
END

PROG TEMPLATE
HEAD
<TEXT>
  RESULTS:
                 ULD/SLD       CALD/CALD (5.7)   CALD/CALD (5.8)
  sig-cd         19.83         19.45             19.83
  sig-sd         456.52        456.92            469.57
  sig-cc         28.67         dito              dito
  sig-sc         469.57        dito              dito
  u-top          218 mm        267 mm            268 mm
  As             21.6 cm2      25.7 cm2          25.1 cm2
                 29.8 cm2      34.1 cm2          33.2 cm2
</TEXT>
END
