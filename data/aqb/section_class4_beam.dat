!#!Info  Example:       Steelsection with Section class 4
!#!Info  Program:       STAR2
!#!Info  Date:          $Date: 2021-11-10 15:43:06 +0100 (Wed, 10 Nov 2021) $

PROG AQUA
HEAD STABILITY OF A HOLLOW BOX COLUMN
NORM EN 199X-200X
ECHO SECT EXTR
STEE 1 S 355
STO#B 326,157.5 ; LET#Y #B/2,(#B-#B(1))/2
STO#H 196,142.8 ; LET#Z #H/2,(#H-#H(1))/2
STO#T 4
SECT 1 TITL 'ORIG. QUERSCHNITT'
PLAT 1 -#B/2 -#H/2 +#B/2 -#H/2 T #T
PLAT 2 +#B/2 -#H/2 +#B/2 +#H/2 T #T
PLAT 3 +#B/2 +#H/2 -#B/2 +#H/2 T #T
PLAT 4 -#B/2 +#H/2 -#B/2 -#H/2 T #T
SECT 2 TITL 'EFFEKT. QUERSCHNITT'
PLAT 11 -#Y    -#Z     -#Y(1)  -#Z    T #T FIXE #B(1)/2-#B
PLAT 12 -#Y(1) -#Z     +#Y(1)  -#Z    T #T TYPE NEFF
PLAT 13 +#Y(1) -#Z     +#Y     -#Z    T #T FIXB #B(1)/2-#B
PLAT 21 +#Y    -#Z     +#Y     -#Z(1) T #T FIXE #H(1)/2-#H
PLAT 22 +#Y    -#Z(1)  +#Y     +#Z(1) T #T TYPE NEFF
PLAT 23 +#Y    +#Z(1)  +#Y     +#Z    T #T FIXB #H(1)/2-#H
PLAT 31 +#Y    +#Z     +#Y(1)  +#Z    T #T FIXE #B(1)/2-#B
PLAT 32 +#Y(1) +#Z     -#Y(1)  +#Z    T #T TYPE NEFF
PLAT 33 -#Y(1) +#Z     -#Y     +#Z    T #T FIXB #B(1)/2-#B
PLAT 41 -#Y    +#Z     -#Y     +#Z(1) T #T FIXE #Z(1)/2-#H
PLAT 42 -#Y    +#Z(1)  -#Y     -#Z(1) T #T TYPE NEFF
PLAT 43 -#Y    -#Z(1)  -#Y     -#Z    T #T FIXB #H(1)/2-#H
END

PROG AQB
HEAD TEST AXIAL LOADING
ECHO c2t extr      $  L-Eff   L-Eff   A[mm2]     sig     AQB
S 1 1 0 N  -200    $  318,0   188,0   4176,0   -47,89  -47,89
S 1 2 0 N  -400    $  257,1   188,0   3688,8  -108,44 -108,44
S 1 3 0 N  -600    $  213,1   188,0   3336,8  -179,81 -179,83
                   $  WITH USN ALL PLATES GET INEFFECIVE PARTS
S 1 4 0 N  -800    $  176,4   156,1   2788,0  -286,94 -286,94
S 1 5 0 N -1000    $  161,2   144,9   2576,8  -388,08 -388,10
NSTR ULTI ; ECHO NSTR EXTR
END

PROG SOFIMSHA
HEAD STABILITY OF A HOLLOW BOX COLUMN
SYST SPAC  GDIR NEGZ  GDIV 1000
NODE 1       FIX PPMZ
NODE 2 Z 8.5 FIX ZP
GRP 1 ; BEAM MESH 1 2 NCS 1 DIV 12
GRP 2 ; BEAM MESH 1 2 NCS 2 DIV 12
END

PROG SOFILOAD
HEAD LOADING
$ IMPERFECTION BUCKLING CURVE A ELASTIC 1/300 ; PLASTIC 1/250
$ IMPERFECTION BUCKLING CURVE C ELASTIC 1/200 ; PLASTIC 1/150
LC 1 ; NODE 2 PG 100.0 ; BEPL 1001 1012 TYPE UZS 1/500 A 50[%]
LC 2 ; NODE 2 PG 100.0 ; BEPL 2001 2012 TYPE UZS 1/300 A 50[%]
END

PROG STAR2
HEAD REFERENCE LOADCASES
CTRL I ; GRP 1 ; LC 100 FACT 1.0 ; LCC 1 ; END
CTRL I ; GRP 2 ; LC 200 FACT 1.0 ; LCC 2 ; END
END

PROG DYNA
HEAD EIGENVALUES
GRP 1 ; CTRL PLC 100 ; EIGE 5 BUSI LC 101
END
HEAD EIGENVALUES
GRP 2 ; CTRL PLC 200 ; EIGE 5 BUSI LC 201
END

PROG STAR2
HEAD LOADCASES WITH INCREASING LOADING
LET#FAKT 0.01,2.00,3.00,3.50,4.00,4.10,4.20,4.30,4.31,4.32 $ 4.33 instable with US1
LOOP#1 FAKT
CTRL II 25
GRP 1 ; LC 110+#1 FACT #FAKT(#1) ; LCC 1
NSTR US1 FMAX 2.0
END
ENDLOOP
END

PROG STAR2
HEAD LOADCASES WITH INCREASING LOADING MANUAL INEFFECTIVITIES
LET#FAKT 0.01,2.00,3.00,3.50,4.00,4.10,4.20,4.30 $ 4.31 instable with US1
LOOP#1 FAKT
CTRL II 25 ; ECHO STEP 1
GRP 2 ; LC 210+#1 FACT #FAKT(#1) ; LCC 2
NSTR US1 FMAX 2.0
END
ENDLOOP
END
